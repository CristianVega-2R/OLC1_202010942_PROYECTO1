import java_cup.runtime.Symbol;
import java.util.LinkedList;
import java_cup.runtime.Scanner;

parser code
{:
    private Symbol s;
    public static LinkedList errores = new LinkedList();
    public void syntax_error(Symbol s){
        errores.add(new Errores(s.left, s.right, (String) s.value));
    }

    public Symbol getS(){
        return this.s;
}
:};

action code
{:
    public static LinkedList conjuntos = new LinkedList();
    Conjunto conjunto = new Conjunto();
    public static LinkedList raices = new LinkedList();
    public static LinkedList tablas = new LinkedList();
    public static LinkedList lexemas = new LinkedList();
    
    Tabla tabla = new Tabla();
    public int idArbol = 0;
    public int idHoja = 1;
:};

terminal    ERROR, conj, abrirllaves, cerrarllaves, dospuntos, puntoycoma,
            porcentaje, menorque, mayorque, comillas, tilde,
            coma, concatenacion, disyuncion, mas, asterisco, interrogacion;

terminal String caracter, cadena, identificador,tcomillasimple,tsaltodelinea,
                guion;

non terminal INICIO,REGLAS,LEXEMAS,CONJUNTO,LEXEMA,NOTACION,ER;

non terminal Arbol EXP;

precedence left concatenacion, disyuncion;
precedence left mas, asterisco, interrogacion;

start with INICIO;

INICIO ::= abrirllaves REGLAS porcentaje porcentaje porcentaje porcentaje LEXEMAS cerrarllaves
            | error;

REGLAS ::=  REGLAS CONJUNTO |
            REGLAS ER |
            CONJUNTO |
            ER;

LEXEMAS ::= LEXEMAS LEXEMA |
            LEXEMA;

CONJUNTO ::=    conj dospuntos identificador:nombre guion mayorque NOTACION puntoycoma {: conjunto.setNombre(nombre); conjuntos.add(conjunto); conjunto = new Conjunto();:};

LEXEMA ::= identificador:nombre dospuntos cadena: cadena puntoycoma {: System.out.println(cadena.replace("\\", "")); lexemas.add(new Lexema(nombre,cadena.replace("\\", ""))); :};

ER ::= identificador:nombre guion mayorque EXP:arbol puntoycoma {:
        Arbol nodo1 = new Arbol("#", idArbol);
        nodo1.setHoja(0);
        tabla.setFila(0, "#");
        idArbol++;
        Arbol nodo2 = new Arbol(".", idArbol);
        nodo2.ramaIzq = arbol;
        nodo2.ramaDer = nodo1;
        nodo2.setAnulabilidad();
        nodo2.setPrimerosAnd();
        nodo2.setUltimosAnd();
        
        tabla.setColumnas(arbol.getUltimos(),nodo1.getPrimeros());
        Raiz raiz = new Raiz(nodo2,nombre);
        tablas.add(tabla);
        tabla = new Tabla();
        raices.add(raiz);

:};    

NOTACION ::=    caracter:inicio tilde caracter:fin {:
                conjunto.setRango(inicio, fin);
 :}|
                caracter:caracter coma NOTACION {:
                conjunto.setCaracter(caracter);
:}|
                guion:caracter coma NOTACION {:
                conjunto.setCaracter(caracter);
:}|
                caracter:caracter {:
                conjunto.setCaracter(caracter);
:}|
                guion:caracter {:
                conjunto.setCaracter(caracter);
:};

EXP ::=  concatenacion EXP:izq EXP:der {: 
        Arbol nodo = new Arbol(".", idArbol);
        idArbol++;
        nodo.ramaIzq = izq;
        nodo.ramaDer = der;
        nodo.setAnulabilidad();
        nodo.setPrimerosAnd();
        nodo.setUltimosAnd();
        tabla.setColumnas(izq.getUltimos(),der.getPrimeros());
        RESULT = nodo;
 :} |
        disyuncion EXP:izq EXP:der {:
        Arbol nodo = new Arbol("|", idArbol);
        idArbol++;
        nodo.ramaIzq = der;
        nodo.ramaDer = izq;
        nodo.setAnulabilidad();
        nodo.setPrimerosOr();
        nodo.setUltimosOr();
        RESULT = nodo;
:} |
        mas EXP:izq {:
        Arbol nodo = new Arbol(".", idArbol);
        idArbol++;

        Arbol asterisco = new Arbol("*", idArbol);
        idArbol++;
        
        izq.idAux = idArbol;
        izq.idHojas = idHoja;
        izq.hojas = tabla;

        Arbol aux = izq.recorrerStack(izq);

        tabla = izq.hojas;
        idArbol = izq.idAux;
        idHoja = izq.idHojas;

        asterisco.ramaIzq = aux;
        asterisco.setAnulabilidad();
        asterisco.setPrimerosUltimos();
        tabla.setColumnas(asterisco.getUltimos(),asterisco.getPrimeros());
        
        nodo.ramaIzq = izq;
        nodo.ramaDer = asterisco;
        nodo.setAnulabilidad();
        nodo.setPrimerosAnd();
        nodo.setUltimosAnd();
        tabla.setColumnas(izq.getUltimos(),asterisco.getPrimeros());

        RESULT = nodo;
:}|
        asterisco EXP:izq {:
        Arbol nodo = new Arbol("*", idArbol);
        idArbol++;
        nodo.ramaIzq = izq;
        nodo.setAnulabilidad();
        nodo.setPrimerosUltimos();
        tabla.setColumnas(izq.getUltimos(),izq.getPrimeros());
        RESULT = nodo;

:}|
        interrogacion EXP:izq {:
        Arbol nodo = new Arbol("|", idArbol);
        idArbol++;

        nodo.ramaIzq = izq;
        Arbol epsilon = new Arbol("epsilon", idArbol);
        epsilon.setHoja(idHoja);
        tabla.setFila(idHoja,"epsilon");
        idHoja++;
        idArbol++;
        nodo.ramaDer = epsilon;
        nodo.setAnulabilidad();
        nodo.setPrimerosOr();
        nodo.setUltimosOr();
        RESULT = nodo;
:}|
        abrirllaves identificador:conjunto cerrarllaves {:
        Arbol nodo = new Arbol(conjunto, idArbol);
        nodo.setHoja(idHoja);
        tabla.setFila(idHoja,conjunto);
        idArbol++;
        idHoja++;
        RESULT = nodo;
:}|
        cadena:caracter {:
        Arbol nodo = new Arbol(caracter, idArbol);
        nodo.setHoja(idHoja);
        tabla.setFila(idHoja,caracter);
        idArbol++;
        idHoja++;
        RESULT = nodo;
:}|     
        tcomillasimple:caracter {:
        Arbol nodo = new Arbol("\"'\"", idArbol);
        nodo.setHoja(idHoja);
        tabla.setFila(idHoja,"\"'\"");
        idArbol++;
        idHoja++;
        RESULT = nodo;
:}|     
        tsaltodelinea:caracter {:
        Arbol nodo = new Arbol("\"\n\"", idArbol);
        nodo.setHoja(idHoja);
        tabla.setFila(idHoja,"\"\n\"");
        idArbol++;
        idHoja++;
        RESULT = nodo;
:};